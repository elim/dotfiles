# Open new Terminal tabs from the command line
#
# Original Author: Justin Hileman (http://justinhileman.com)
# https://gist.github.com/bobthecow/757788
#
# Usage:
#     tab                   Opens the current directory in a new tab
#     tab [PATH]            Open PATH in a new tab
#     tab [CMD]             Open a new tab and execute CMD
#     tab [PATH] [CMD] ...  You can probably guess

# Only for the iTerm or the Gnome-terminal users.
[[ "${OSTYPE}" =~ ^(darwin|linux) ]] || return
(( $+commands[tmux] )) || return

local new_session_script=''
local cdto="$PWD"
local session_name=""
local tab_name=""
local args="$@"

if [[ -d "$1" ]]; then
  cdto=$(realpath "${1}")
  args="${@:2}"
  tab_name="${cdto##*/}"
else
  tab_name="${args}"
fi
session_name="${${${tab_name}//\./-}// /_}"

if [[ -n "$args" ]]; then
  new_session_script="/tmp/tmux-new-session-${RANDOM}.zsh"
  {
    echo "#!${commands[zsh]}"
    echo "$args"
  } > ${new_session_script}
  chmod +x ${new_session_script}
fi

case ${OSTYPE} in
  darwin)
    /usr/bin/osascript \
      -l JavaScript ${ZDOTDIR}/functions/tab.js \
        ${commands[tmux]} \
        ${cdto} \
        ${tab_name} \
        ${session_name} \
        ${new_session_script}
    ;;
  linux-gnu)
    gnome-terminal \
      --tab \
      --title ${tab_name} \
      -- sh -c "
        cd ${cdto} &&
          exec tmux new-session \\
            -ADs ${session_name} ${new_session_script} \\; \\
            set-option set-titles-string '${tab_name}'"
    ;;
esac

# Local Variables:
# mode: sh
# sh-shell: zsh
# End:
