#
# http://d.hatena.ne.jp/mollifier/20090814/p1
#

setopt prompt_subst

if is-at-least 4.3.7; then
  {
    autoload -Uz vcs_info

    zstyle ':vcs_info:*' enable git svn hg bzr
    zstyle ':vcs_info:bzr:*' use-simple true
    zstyle ':vcs_info:(svn|bzr):*' branchformat '%b:r%r'
    zstyle ':vcs_info:*' formats ' [%b]'
    zstyle ':vcs_info:*' actionformats ' [%b|%a]'

    _invoke_vcs_info () {
      psvar=()
      LANG=en_US.UTF-8 vcs_info
      [[ -n "$vcs_info_msg_0_" ]] && psvar[1]="$vcs_info_msg_0_"
    }
    add-zsh-hook precmd _invoke_vcs_info
  }
fi

() {
  local lib=/usr/local/etc/bash_completion.d/git-prompt.sh

  if [[ -f $lib ]]; then
    GIT_PS1_SHOWUNTRACKEDFILES=1
    GIT_PS1_SHOWDIRTYSTATE=1
    GIT_PS1_SHOWUPSTREAM="verbose"
    source $lib

    _invoke_git_prompt () {
      _git_ps1=''
      LANG=en_US.UTF-8 _git_ps1=$(__git_ps1)

      if [[ -n "$_git_ps1" ]]; then
        psvar="${_git_ps1}"
      fi
    }
    add-zsh-hook precmd _invoke_git_prompt
  fi
}

() {
  local host_color

  if [[ -n "$SSH_CONNECTION" ]]; then
    host_color='red'
  else
    host_color='green'
  fi

  PROMPT="%F{$host_color}%n@%m%f:%F{yellow}%c%F{red}%1(v|%1v|)%f %(#.#.$) "
  RPROMPT="%F{yellow}[%~]%f"
}

# Local Variables:
# mode: sh
# End:
