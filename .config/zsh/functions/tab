# Open new Terminal tabs from the command line
#
# Original Author: Justin Hileman (http://justinhileman.com)
# https://gist.github.com/bobthecow/757788
#
# Usage:
#     tab                   Opens the current directory in a new tab
#     tab [PATH]            Open PATH in a new tab
#     tab [CMD]             Open a new tab and execute CMD
#     tab [PATH] [CMD] ...  You can probably guess

# Only for the iTerm users

[[ "${OSTYPE}" =~ ^darwin* ]] || return
(( $+commands[tmux] )) || return

local new_session_script=''
local cdto="$PWD"
local session_name=""
local args="$@"

if [[ -d "$1" ]]; then
  cdto=$(realpath "${1}")
  args="${@:2}"
  session_name="${${${cdto##*/}//\./-}// /_}"
else
  session_name="${${${args}//\./-}// /_}"
fi

if [[ -n "$args" ]]; then
  new_session_script="/tmp/tmux-new-session-${RANDOM}.zsh"
  {
    echo "#!${commands[zsh]}"
    echo "$args"
  } > ${new_session_script}
  chmod +x ${new_session_script}
fi

/usr/bin/osascript -l JavaScript \
                   ${ZDOTDIR}/functions/tab.js \
                   ${commands[tmux]} \
                   ${cdto} \
                   ${session_name} \
                   ${new_session_script}

# Local Variables:
# mode: sh
# sh-shell: zsh
# End:
